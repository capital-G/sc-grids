TITLE:: ScGrids
summary:: Topographic drum pattern sequencer
CATEGORIES:: Collections, Pattern
RELATED:: Classes/Pgrids, Classes/PgridsValue, Classes/Tdef

DESCRIPTION::
SuperCollider port of drum sequencer Eurorack module Grids by Mutable Instruments used in form of a link::Classes/Pattern:: via link::Classes/Pbind::.

If you want to use it in a link::Classes/Pbind:: check out the the pattern implementation link::Classes/Pgrids:: and link::Classes/PgridsValue::.

A GUI which mimics the handling of the original module can be found in the examples.

For more information check the manual of the module https://mutable-instruments.net/modules/grids/manual/


CLASSMETHODS::

METHOD:: calculateLevel

Calculates a level for an instrument according to the parameters of the algorithm.

ARGUMENT:: instrument
One of 'kick', 'hihat' or 'snare'.

ARGUMENT:: curBeat
The current position in the beat between [0, 31].

ARGUMENT:: x
x position on the drum map - between [0, 1]

ARGUMENT:: y
y position on the drum map - between [0, 1]

ARGUMENT:: bias
adds an offset to the signal and mimics therefore the chaos parameter
of the original module.

RETURNS::
Level between [0, 1] of instrument at beat.

EXAMPLES::

Example of multiple Tdefs mapped to the sequences of ScGrids which parame are controlable via a GUI.

CODE::
(
// some basic drum synths
SynthDef(\kick, {|out|
	var sig = SinOsc.ar(XLine.ar(start: 600, end: 50, dur: 0.1));
	var env = EnvGen.kr(Env.perc(releaseTime: 0.25, curve: 5.0), doneAction: Done.freeSelf);
	Out.ar(out, Pan2.ar(sig*env*\amp.kr(0.5)));
}).add;

SynthDef(\hh, {|out|
	var sig = WhiteNoise.ar*EnvGen.kr(Env.perc(releaseTime: \release.kr(0.2)), doneAction: Done.freeSelf)*\amp.kr(0.5);
	sig = LPF.ar(sig, freq: \ffreq.kr(4000));
	Out.ar(out, Pan2.ar(sig, \pos.kr(0.0)));
}).add;
)

(
// setup a clock
t = TempoClock.default;
t.tempo = 130/60.0;

// tdefs to play patterns
Tdef(\hh, {|env|
	inf.do({|i|
		var level = ScGrids.calculateLevel(
			instrument: \hihat,
			curBeat: i,
			x: env.x,
			y: env.y,
		);
		if(level>=(1-(env.density)), {
			(
				instrument: \hh,
				amp: level,
				release: level.linlin(0, 1.0, 0.0, 0.04),
				ffreq: 1000 + (exprand(1000, 6000) * level),
			).play;
		});
		(0.125).wait;
	});
}).clock_(t);

Tdef(\k, {|env|
	inf.do({|i|
		var level = ScGrids.calculateLevel(
			instrument: \kick,
			curBeat: i,
			x: env.x,
			y: env.y,
		);
		if(level>=(1-(env.density)), {
			(
				instrument: \kick,
				amp: level,
			).play;
		});
		(0.125).wait;
	});
}).clock_(t);

Tdef(\sd, {|env|
	inf.do({|i|
		var level = ScGrids.calculateLevel(
			instrument: \snare,
			curBeat: i,
			x: env.x,
			y: env.y,
		);
		if(level>=(1-(env.density)), {
			(
				instrument: \hh,
				amp: level,
				release: level.linlin(0, 1.0, 0.0, 0.2),
				ffreq: 4000 + (exprand(1000, 4000) * level),
			).play;
		});
		(0.125).wait;
	});
}).clock_(t);

// set default values for tdefs
[\hh, \k, \sd].do({|instrument|
	[\density, \x, \y].do({|val|
		Tdef(instrument).set(val, 0.5)
	});
});

// GUI for tdefs
w = Window("SC grids", Rect(100, 100, 400, 580));

[\hh, \k, \sd].do({|instrument, num|
	Slider2D(w, Rect(20, 20 + (180*num), 150, 150))
	.x_(0.5)
	.y_(0.5)
	.action_({|sl|
		Tdef(instrument).set(\x, sl.x);
		Tdef(instrument).set(\y, sl.y);
	});

	StaticText(w, Rect(200, 20 + (180*num), 100, 20)).string = instrument;
	StaticText(w, Rect(200, 60 + (180*num), 150, 20)).string = "density";
	
	Slider(w, Rect(200, 95 + (180*num), 150, 20))
	.value_(0.5)
	.action_({|sl|
		Tdef(instrument).set(\density, sl.value);
	});
	
	Button(w, Rect(200, 120 + (180*num), 100, 40))
	.states_([
		["play", Color.black, Color.green],
		["stop", Color.black, Color.red],
	])
	.action_({
		var tdef = Tdef(instrument);
		if(tdef.isPlaying, {tdef.stop}, {tdef.play});
	});
});

w.front;
)

::